apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Firebase.
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

import java.util.regex.Pattern

import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

def outDir = project.getLayout().getBuildDirectory()

android {
    compileSdk = 36
    compileSdkMinor 1
    buildToolsVersion '36.1.0'

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        applicationId "com.fezrestia.android.webviewwindow"
        minSdk 34
        targetSdk 36
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }

    // Define APK name and replace.
    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            if (output.outputFileName.endsWith(".apk")) {
                def name = output.outputFileName - ".apk"
                def ver = variant.versionName

                if (variant.name == "release") {
                    output.outputFileName = "${name}_${ver}.apk"
                }
            }
        }
    }

    productFlavors {
    }
    lint {
        htmlOutput file("$outDir/reports/test-results-lint.html")
        lintConfig file('lint.xml')
        xmlOutput file("$outDir/reports/test-results-lint.xml")
    }
    ndkVersion '29.0.14206865'
    namespace 'com.fezrestia.android.webviewwindow'

    incrementVersionCode()
}

tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

def incrementVersionCode() {
    def file = file("src/main/AndroidManifest.xml")
    def text = file.getText()

    // Update version code.
    def codePattern = Pattern.compile("android:versionCode=\"(\\d+)\"")
    def codeMatcher = codePattern.matcher(text)
    codeMatcher.find()
    def code = Integer.parseInt(codeMatcher.group(1))
    def newCode = code + 1
    def codeUpdatedText = codeMatcher.replaceAll("android:versionCode=\"" + newCode + "\"")

    // Update version name.
    def namePattern = Pattern.compile("android:versionName=\"(\\d+)\\.(\\d+)\\.(\\d+)-\\d+\"")
    def nameMatcher = namePattern.matcher(codeUpdatedText)
    nameMatcher.find()
    def major = Integer.parseInt(nameMatcher.group(1))
    def minor = Integer.parseInt(nameMatcher.group(2))
    def patch = Integer.parseInt(nameMatcher.group(3))
    def newName = "android:versionName=\"" + major + "." + minor + "." + patch + "-" + newCode + "\""
    def nameUpdatedText = nameMatcher.replaceAll(newName)

    file.write(nameUpdatedText)
}

dependencies {
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.3.0'
    implementation 'androidx.browser:browser:1.9.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'commons-validator:commons-validator:1.10.1'
    implementation 'androidx.activity:activity-ktx:1.12.2'
    implementation 'androidx.fragment:fragment-ktx:1.8.9'
    implementation 'androidx.appcompat:appcompat:1.7.1'

    // Firebase.
    implementation platform('com.google.firebase:firebase-bom:34.7.0')
    implementation 'com.google.firebase:firebase-analytics:23.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:20.0.3'

}

repositories {
    google()
    mavenCentral()
}
